plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

version '0.0.0'
def mainClass = 'com.dansoftware.libraryapp.main.Main'

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileJava {
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx.runtime=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.controls/com.sun.javafx.scene.control.skin.resources=ALL-UNNAMED"]
    options.compilerArgs << '-Xlint:unchecked'
}

compileTestJava {
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx.runtime=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.base/com.sun.javafx=ALL-UNNAMED"]
    options.compilerArgs += ["--add-exports", "javafx.controls/com.sun.javafx.scene.control.skin.resources=ALL-UNNAMED"]
}

repositories {
    mavenCentral()

    //JitPack repository
    maven { url "https://jitpack.io" }

    //repository for JMetro
    maven { url "https://dl.bintray.com/dukke/maven" }

    //repository for FontawesomeFX
    maven { url "https://dl.bintray.com/jerady/maven" }

    //repository for markdown-javafx-renderer
    maven {
        url "http://sandec.bintray.com/repo"
    }

    //repository for JUnique
    maven {
        url "https://raw.githubusercontent.com/terjedahl/junique/master/maven2"
    }
}

processResources {

    //we accept the .fxml and .css resources from the java source set
    from('src/main/java') {
        include '**/*.fxml'
        include '**/*.css'
    }
}

configurations {
    //for holding the dependencies
    libraries

    //we exclude the javafx group
    libraries.exclude group: 'org.openjfx'
}

test {
    useJUnitPlatform()
}
dependencies {
    //Kotlin
    libraries "org.jetbrains.kotlin:kotlin-stdlib"

    //logging
    libraries group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    libraries group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    libraries group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    //JUnit 5
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')

    /*** UTILITY LIBRARIES ***/
    libraries 'com.google.code.gson:gson:2.8.6'
    libraries 'org.jetbrains:annotations:19.0.0'
    libraries 'org.reflections:reflections:0.9.12'
    libraries 'commons-io:commons-io:2.6'
    libraries 'org.apache.commons:commons-lang3:3.10'
    libraries 'commons-collections:commons-collections:3.2.2'
    libraries 'com.github.Dansoftowner:jFileGoodies:1.2.6'
    libraries 'com.github.oshi:oshi-core:5.3.4'

    // OkHttp
    libraries 'com.squareup.okhttp3:okhttp:4.9.0'

    // Google books api client library
    libraries 'com.google.apis:google-api-services-books:v1-rev20201021-1.31.0'

    /*** JAVA FX LIBRARIES ***/

    libraries 'com.jfoenix:jfoenix:9.0.9'
    libraries 'org.jfxtras:jmetro:11.6.14'
    libraries 'org.controlsfx:controlsfx:11.0.1'

    //Modified WorkBenchFX
    //Original: 'com.dlsc.workbenchfx:workbenchfx-core:11.1.0'
    libraries 'com.github.Dansoftowner:workbenchfx-fork:11.1.7'

    libraries 'de.jensd:fontawesomefx-commons:11.0'
    libraries 'de.jensd:fontawesomefx-fontawesome:4.7.0-11'
    libraries 'io.github.typhon0:AnimateFX:1.2.1'
    libraries "com.sandec:mdfx:0.1.6"
    libraries 'com.github.Dansoftowner:FXTaskbarProgressBar:v11.3'
    libraries 'com.github.Dansoftowner:SegmentDialogFX:2.0'



    //nitrite database
    libraries 'org.dizitart:nitrite:3.4.2'

    //junique
    libraries 'it.sauronsoftware:junique:1.0.4'

    //libraries from the local file system
    libraries fileTree(dir: 'external-libraries', include: '*.jar')

    // libraries = implementation
    configurations.implementation.extendsFrom(configurations.libraries)
}


jar {
    //we exclude the javafx package from the jar
    exclude('javafx/**')
    //we exclude junit from the jar
    exclude('junit/**')
    //we exclude unnecessary javafx-swt.jar resource
    exclude('javafx-swt.jar')

    manifest {
        attributes 'Main-Class': mainClass
    }

    from {
        // we include all libraries in a single jar
        configurations.libraries.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

